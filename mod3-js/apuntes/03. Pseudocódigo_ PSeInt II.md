# 5. Pseudocódigo: PSeInt II

Veamos cómo funciona cada una de las diferentes instrucciones que hemos visto en el apartado anterior. Cada explicación viene acompañada de un ejemplo, puedes copiar el código y pegarlo en PSeInt para comprobar que funciona. 

¡No dudes en hacer cualquier modificación dentro del programa! Si no juegas con el código será complicado que entiendas su funcionamiento. ¡Si aún no has descargado PSeInt hazlo! ¿A qué esperas? ;)

&nbsp;

## Escribir

Muestra un valor o una cadena de texto (string) por consola.

```javascript
Algoritmo aprendiendo_pseudocodigo
    Escribir "Hola, ¿cómo te llamas?"
FinAlgoritmo
```

&nbsp;


## Leer
 
Permite al usuario introducir un valor (un número, una palabra, una frase, etc.) por consola y almacenarlo en una variable.

> Una variable es como una caja que almacena un valor en su interior. Toda variable debe tener un nombre asignado que será definido por el programador y debería guardar relación con el contenido de la variable. En este caso hemos llamado a la variable nombre dado que es el valor que almacenaremos dentro.

```javascript
Algoritmo aprendiendo_pseudocodigo
    Escribir "Hola, ¿cómo te llamas?"
    Leer nombre
    Escribir "¡Bienvenid@ ", nombre, "!"
FinAlgoritmo
```

&nbsp;


## Asignar 

Asigna un valor a una variable.

> En este caso no es el usuario quien decide qué nombre se va a almacenar si no que es el programador quién lo define dentro del código. Fíjate también como aprovechamos la instrucción Escribir para mostrar un mensaje personalizadoque incluirá el contenido de la variable nombre.

```javascript
Algoritmo aprendiendo_pseudocodigo
    nombre <- "Rebeca"
    Escribir "¡Bienvenid@ ", nombre, "!"
FinAlgoritmo
```

&nbsp;


## Si-Entonces (if...else)

Analiza una condición. Si se cumple (true) se ejecuta la instrucción A, de lo contrario (false) se ejecuta la instrucción B.

> Podemos dejar comentarios en nuestro código para ayudarnos a entender qué está pasando. Estos comentarios serán ignorados durante la ejecución, no nos causarán ningún problema. Para añadir un comentario basta con iniciar la línea con //.

```javascript
Algoritmo aprendiendo_pseudocodigo
    edad <-  18
    Si edad Es Menor Que 18 Entonces         
      // Instrucción A (esto es un comentario)
      Escribir  "La persona es menor de edad"
  SiNo
     // Instrucción B (esto es otro comentario)
     Escribir  "La persona es mayor de edad"
  Fin Si
FinAlgoritmo
```

&nbsp;


## Según (switch)

Permite ejecutar diferentes casos en función del valor de una variable.

  > El nombre de una variable no debería contener caracteres que no esten presentes en el alfabeto inglés (acentos, ñ, ç, etc.). En PSeInt esto no es relevante, pero os recomiendo que os toméis esto como costumbre.

```javascript
Algoritmo aprendiendo_pseudocodigo
    miValor <-  1
    Según miValor Hacer 
      0:
        Escribir  "Perro"         
      1:
        Escribir "Gato"
      2:
        Escribir "Ratón"
      De Otro Modo:
        Escribir "El valor introducido no se corresponde a ningún caso contemplado"
    Fin Segun
FinAlgoritmo
```

&nbsp;


## Mientras (while)

Analiza una condición. Mientras que esa condición se cumpla (true) el bloque de código se repetirá una y otra vez. 

  > Es muy importante que la condición deje de cumplirse en algún momento, de lo contrario tendremos lo que se conoce como bucle infinito. Para ello vamos a sumar a la variable miValor una unidad en cada repetición. De esta forma llegará un momento en que miValor deje de ser menor que 10

```javascript
Algoritmo aprendiendo_pseudocodigo
  miValor <-  1
  Mientras miValor Es Menor Que 10 Hacer 
    Escribir miValor   
    // Incrementamos en 1 el valor de la variable "miValor"
    miValor <- miValor + 1
  Fin Mientras
FinAlgoritmo
```

&nbsp;


## Para (for)

Analiza una condición. Mientras que esa condición se cumpla (true) el bloque de código se repetirá una y otra vez. Tras cada repetición se realizará automáticamente un cambio en la variable de la condición que el programador debe definir.

  > El resultado será exactamente el mismo que el ejemplo anterior con Mientras. No es necesario crear la variable antes, lo hacemos dentro del propio Para. Le decimos que miValor es igual a 1, y que el bucle se repetirá hasta que miValor sea mayor que 9. Con Paso 1 quiere decir que tras cada repetición se incrementará el valor de miValor en 1.

```javascript
Algoritmo aprendiendo_pseudocodigo
  Para miValor <- 1 Hasta 9 Con Paso 1 Hacer
    Escribir  miValor
  Fin Para
FinAlgoritmo
```